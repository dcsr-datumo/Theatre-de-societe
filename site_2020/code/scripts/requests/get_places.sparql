PREFIX kb: <http://www.knora.org/ontology/knora-base#>
PREFIX tds: <http://www.knora.org/ontology/0103/theatre-societe#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT
distinct ?id 
?name
(sample(distinct ?coord_candidate) as ?coord) 
(group_concat(distinct ?notice;
        separator=", ") as ?notices)
(min(?dateStr) as ?minDate)
(max(?dateStr) as ?maxDate)
WHERE {
    ?id a tds:Place .
    ?id kb:isDeleted "false"^^xsd:boolean .
    OPTIONAL {
        ?id tds:placeHasName ?nameValue .
        ?nameValue kb:valueHasString ?name .
    }
    OPTIONAL {
        ?id tds:placeHasCoordinates ?coordValue .
        ?coordValue kb:isDeleted "false"^^xsd:boolean .
        ?coordValue kb:valueHasString ?coord_candidate .
    }
    OPTIONAL {
        ?id tds:placeHasNotice ?noticeValue .
        ?noticeValue kb:isDeleted "false"^^xsd:boolean .
        ?noticeValue kb:valueHasString ?notice .
    }
    OPTIONAL {
        ?representation tds:representationHasPlace ?id .
        OPTIONAL {
            ?representation tds:representationHasDate ?date .
            ?date kb:valueHasString ?dateStr .
        }
    }
}
group by ?id ?name
#limit 10