PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX tds: <http://www.knora.org/ontology/0103/theatre-societe#>
PREFIX kb: <http://www.knora.org/ontology/knora-base#>

SELECT
    distinct ?id ?title 
    (MIN(?nameV) as ?name)
    (group_concat(?author; SEPARATOR=", ") as ?authorIds)
WHERE {
    ?id a tds:Work .
    ?id kb:isDeleted "false"^^xsd:boolean .
    OPTIONAL {
        ?id tds:workHasTitle ?titleValue .
        ?titleValue kb:valueHasString ?title .
    }
    OPTIONAL {
        ?id tds:workHasAuthor ?author .
        ?author kb:isDeleted "false"^^xsd:boolean .
        OPTIONAL {
            ?author tds:hasFamilyName ?nameValue .
            ?nameValue kb:valueHasString ?nameV .
        }
    }
}
GROUP BY ?id ?title
order by ?title